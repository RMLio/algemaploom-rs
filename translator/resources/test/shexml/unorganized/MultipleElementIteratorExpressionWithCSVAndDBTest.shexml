PREFIX : <http://example.com/>
PREFIX xs: <http://www.w3.org/2001/XMLSchema#>
SOURCE films_xml_file <data/films.xml>
SOURCE films_json_file <data/films.json>
SOURCE films_csv_file <data/films.csv>
SOURCE films_db_connection <jdbc:sqlite:src/test/resources/films.sqlite>
ITERATOR film_xml <xpath: //film> {
    FIELD id <@id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <directors/director>
}
ITERATOR film_json <jsonpath: $.films[*]> {
    FIELD id <id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <director>
}
ITERATOR film_csv <csvPerRow> {
    FIELD id <id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <director>
}
ITERATOR film_db <sql: SELECT * FROM films;> {
    FIELD id <id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <director>
}
EXPRESSION films <films_xml_file.film_xml UNION films_json_file.film_json UNION films_csv_file.film_csv
               UNION films_db_connection.film_db>

:Films :[films.id] {
    :type :Film ;
    :name [films.name] @en ;
    :year [films.year] xs:gYear ;
    :country [films.country] ;
    :director [films.directors] ;
}
