PREFIX : <http://example.com/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX schema: <http://schema.org/>
SOURCE films_xml_file <data/films.xml>
SOURCE films_json_file <data/films.json>
FUNCTIONS helper <scala: https://raw.githubusercontent.com/herminiogg/ShExML/enhancement-%23121/src/test/resources/functions.scala>
ITERATOR film_xml <xpath: //film> {
    FIELD id <@id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <crew/directors/director>
    FIELD screenwritters <crew//screenwritter>
    FIELD music <crew/music>
    FIELD photography <crew/photography>
    ITERATOR actors <cast/actor> {
        FIELD name <name>
        FIELD role <role>
        FIELD film <../../@id>
    }
    ITERATOR actresses <cast/actress> {
        FIELD name <name>
        FIELD role <role>
        FIELD film <../../@id>
    }
}
ITERATOR film_json <jsonpath: $.films[*]> {
    PUSHED_FIELD id <id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <crew.director>
    FIELD screenwritters <crew.screenwritter>
    FIELD music <crew.music>
    FIELD photography <crew.cinematography>
    ITERATOR actors <cast[*]> {
        FIELD name <name>
        FIELD role <role>
        POPPED_FIELD film <id>
    }
}
EXPRESSION films <films_xml_file.film_xml UNION films_json_file.film_json>

:Films :[films.id] {
    :name [helper.allCapitals(films.name)] ;
    :year [helper.addOne(films.year)] ;
    :countryOfOrigin dbr:[films.country] ;
    :director dbr:[films.directors] ;
    :screenwritter dbr:[films.screenwritters] ;
    :screenwritterName [helper.getName(films.screenwritters)] ;
    :titleYear [helper.nameAndYear(films.name, films.year)] ;
    :musicBy dbr:[films.music] ;
    :cinematographer dbr:[films.photography] ;
    :actor @:Actor ;
    :actor @:Actress ;
}

:Actor :[films.actors.name] {
    :name [helper.getName(films.actors.name)] ;
    :surname [helper.getSurname(films.actors.name)] ;
    :nameParts [helper.getNameParts(films.actors.name)] ;
    :appear_on :[films.actors.film] ;
}

:Actress :[films.actresses.name] {
    :name [helper.getName(films.actresses.name)] ;
    :surname [helper.getSurname(films.actresses.name)] ;
    :nameParts [helper.getNameParts(films.actresses.name)] ;
    :appear_on :[films.actresses.film] ;
}
