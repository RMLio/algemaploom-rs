PREFIX : <http://example.com/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX schema: <http://schema.org/>
SOURCE films_xml_file <data/films.xml>
SOURCE films_json_file <data/films.json>
ITERATOR film_xml <xpath: //film> {
    FIELD id <@id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <crew/directors/director>
    FIELD screenwritters <crew//screenwritter>
    FIELD music <crew/music>
    FIELD photography <crew/photography>
}
ITERATOR film_json <jsonpath: $.films[*]> {
    PUSHED_FIELD id <id>
    FIELD name <name>
    FIELD year <year>
    FIELD country <country>
    FIELD directors <crew.director>
    FIELD screenwritters <crew.screenwritter>
    FIELD music <crew.music>
    FIELD photography <crew.cinematography>
}
FUNCTIONS helper <scala: https://raw.githubusercontent.com/herminiogg/ShExML/enhancement-%23122/src/test/resources/functions.scala>
EXPRESSION films <films_xml_file.film_xml UNION films_json_file.film_json>

:Films :[films.id IF helper.isBefore2010(films.year)] {
    :name [films.name] ;
    :year [films.year] ;
    :countryOfOrigin [films.country IF helper.outsideUSA(films.country)] ;
    :director [films.directors] ;
    :screenwritter [films.screenwritters] ;
    :musicBy [films.music] ;
    :cinematographer [films.photography] ;
}
